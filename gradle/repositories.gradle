repositories {
    maven {
        url "https://dl.google.com/dl/android/maven2"
        mavenContent { releasesOnly() }
        content {
            includeGroupByRegex "androidx.*"
            includeGroupByRegex "android\\.arch.*"
            includeGroupByRegex "com\\.android.*"
            includeGroupByRegex "com\\.google\\.android\\.(gms|material).*"
            excludeModule "com.android.tools", "r8"
        }
    }

    jcenter {
        mavenContent { releasesOnly() }
        content {
            excludeGroupByRegex "androidx.*"
            excludeGroupByRegex "android\\.arch.*"
            excludeGroupByRegex "com\\.android.*"
            excludeGroupByRegex "com\\.google\\.android\\.(gms|material).*"
            excludeGroupByRegex "com\\.github\\.(?!bumptech|rubensousa|shyiko).*"
        }
    }

    maven {
        url "https://jitpack.io"
        mavenContent { releasesOnly() }
        content { includeGroupByRegex "com\\.github.*" }
    }

    maven {
        url "https://dl.bintray.com/kotlin/kotlin-eap"
        content { includeGroupByRegex "org\\.jetbrains\\.kotlin.*" }
    }

    maven {
        url "https://storage.googleapis.com/r8-releases/raw"
        content { includeModule "com.android.tools", "r8" }
    }

    maven {
        url  "https://dl.bintray.com/drummer-aidan/maven"
        content { includeGroupByRegex "com\\.afollestad.*" }
    }

    maven {
        url "https://plugins.gradle.org/m2"
        mavenContent { releasesOnly() }
    }

    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
        mavenContent { snapshotsOnly() }
    }
}
